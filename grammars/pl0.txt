program = block "." ;

block = [ "const" identifier "=" number {"," identifier "=" number} ";"]
        [ "var" identifier {"," identifier} ";"]
        { "procedure" identifier ";" block ";" }
        statement ;

statement = [ identifier ":=" expression | "call" identifier
              | "?" identifier | "!" expression
              | "begin" statement {";" statement } "end" 
              | "if" condition "then" statement 
              | "while" condition "do" statement ];

condition = "odd" expression |
            expression ("="|"#"|"<"|"<="|">"|">=") expression ;

expression = term { ("+"|"-") term};

term = factor {("*"|"/") factor};

factor = identifier | number | ("+"|"-") factor | "(" expression ")";
